class "Класс для работы с индексами";
import classes Functions.Variable;

inclass


  -- обрабатывает смещение, закольцовывает отрицательный индекс
  func handle_offset(_offset, _limit: integer): integer;

    -- если offset нулевой или положительный, возьмём его как есть
    -- если отрицательный, возьмём его считая с конца
    if _offset >= 0 then
      result = _offset;

    elsif _offset < 0 then
      result = _limit +_offset +1;
    end;

    -- после этого если offset меньше 1, приведём его к 1
    if result < 1 then result = 1; end;

    return result;
  end;


  -- обрабатывает длину, закольцовывает отрицательный индекс
  func handle_length(_offset: integer; _length: variant; _limit: integer): integer;

    -- если длина это nil, возвращаем длину от offset до limit
    if var_identical(_length, nil) then return _limit -_offset +1; end;

    -- обработаем положительную, отрицательную и нулевую длину
    if _length > 0 then

      -- если длина выходит за limit, возьмём длину от offset до limit
      if _offset +_length -1 > _limit then
        result = _limit -_offset +1;
      else
        result = _length;
      end;
    elsif _length < 0 then

      -- если длина выходит за offset, возьмём длину 0
      if _limit +_length < _offset then
        result = 0;
      else
        result = _limit -_offset +1 +_length;
      end;
    else

      result = 0;
    end;

    return result;
  end;
end