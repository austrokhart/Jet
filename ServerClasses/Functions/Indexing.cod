class "Класс для работы с индексами";
import classes Functions.Variable;

inclass


  -- обрабатывает смещение, закольцовывает отрицательный индекс
  func handle_offset(offset, limit: integer): integer;

    -- если offset нулевой или положительный, возьмём его как есть
    -- если отрицательный, возьмём его считая с конца
    if offset >= 0 then
      result = offset;

    elsif offset < 0 then
      result = limit +offset +1;
    end;

    -- после этого если offset меньше 1, приведём его к 1
    if result < 1 then result = 1; end;

    return result;
  end;


  -- обрабатывает длину, закольцовывает отрицательный индекс
  func handle_length(offset: integer; length: variant; limit: integer): integer;

    -- если длина это nil, возвращаем длину от offset до limit
    if var_identical(length, nil) then return limit -offset +1; end;

    -- обработаем положительную, отрицательную и нулевую длину
    if length > 0 then

      -- если длина выходит за limit, возьмём длину от offset до limit
      if offset +length -1 > limit then
        result = limit -offset +1;
      else
        result = length;
      end;
    elsif length < 0 then

      -- если длина выходит за offset, возьмём длину 0
      if limit +length < offset then
        result = 0;
      else
        result = limit -offset +1 +length;
      end;
    else

      result = 0;
    end;

    return result;
  end;
end