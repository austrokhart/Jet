class "Класс для работы с переменными";
import classes Functions.Variable, Functions.Array, Functions.Class, Functions.Object;

inclass


  type var_type = (
    TYPE_STRING  = 1,
    TYPE_INTEGER = 2,
    TYPE_REAL    = 3,
    TYPE_BOOLEAN = 4,
    TYPE_DATE    = 5,
    TYPE_OBJECT  = 6,
    TYPE_VARIANT = 7,
    TYPE_ARRAY   = 8,
    TYPE_CLASS   = 9,
    TYPE_NIL     = 10
  );


  func var_to_int(const value: variant): integer;
    
    return Kernel.Mathematic.int(Kernel.Mathematic.trunc(value));
  end;

  func var_to_real(const value: variant): real;
    
    return Kernel.Mathematic.num(value);
  end;


  func var_to_dump(const _value: variant): string;


    var _escaper: Escaper;


    func to_str(_value: variant): string;


      func from_nil(_value: variant): string;
      
        return "(nil) nil";
      end;

      func from_boolean(_value: boolean): string;
      
        return "(boolean) " +if (_value, "true", "false");
      end;

      func from_integer(_value: integer): string;
      
        return "(integer) " +str(_value);
      end;

      func from_real(_value: real): string;
      
        return "(real) " +str(_value);
      end;

      func from_string(_value: string): string;
      
        return '(string) "' +_escaper.escape(_value) +'"';
      end;

      func from_date(_value: string): string;
      
        return '(date) ' +str(_value);
      end;

      func from_array(_value: variant[]): string;

        var _values: variant[];

        _values = _value;

        with array_map(_values) do
          while __next do __apply(to_str(__item)); end; __assign(_values);
        end;

        return '(array) [' +array_implode(_values, ', ') +']';
      end;

      func from_class(_value: class): string;

        return '(class) ' +class_name(_value);
      end;

      func from_object(_value: Object): string;
        
        var _properties: string[];

        _properties = object_props(_value);

        with array_filter(_properties) do
          while __next do __apply(object_has_own_member(_value, __item)); end; __assign(_properties);
        end;

        with array_map(_properties) do
          while __next do __apply(__item +': ' +to_str(object_get_prop(_value, __item))); end; __assign(_properties);
        end;

        return '(object) ' +class_name(class_from(_value)) +' {' +array_implode(_properties, ', ') +'}';
      end;

      func from_unknown(_value: variant): string;
        
        return '(unknown) ' +str(_value);
      end;


      if var_is_nil(_value) then
        return from_nil(_value);

      elsif var_is_boolean(_value) then
        return from_boolean(_value);

      elsif var_is_integer(_value) then
        return from_integer(_value);

      elsif var_is_real(_value) then
        return from_real(_value);

      elsif var_is_string(_value) then
        return from_string(_value);

      elsif var_is_date(_value) then
        return from_date(_value);

      elsif var_is_array(_value) then
        return from_array(_value);

      elsif var_is_class(_value) then
        return from_class(_value);

      elsif var_is_object(_value) then
        return from_object(_value);

      else
        return from_unknown(_value);
      end;
    end;


    _escaper = Escaper.new;

    return to_str(_value);
  end;


  func var_is_string(const value: variant): boolean;

    return Kernel.System.varType(value) = TYPE_STRING;
  end;

  func var_is_integer(const value: variant): boolean;
    
    return Kernel.System.varType(value) = TYPE_INTEGER;
  end;

  func var_is_real(value: variant): boolean;
    
    return Kernel.System.varType(value) = TYPE_REAL;
  end;

  func var_is_number(value: variant): boolean;
    
    return var_is_integer(value) or var_is_real(value);
  end;

  func var_is_boolean(value: variant): boolean;
    
    return Kernel.System.varType(value) = TYPE_BOOLEAN;
  end;

  func var_is_date(value: variant): boolean;
    
    return Kernel.System.varType(value) = TYPE_DATE;
  end;

  func var_is_object(value: variant): boolean;
    
    return Kernel.System.varType(value) = TYPE_OBJECT;
  end;

  func var_is_variant(value: variant): boolean;
    
    return Kernel.System.varType(value) = TYPE_VARIANT;
  end;

  func var_is_array(value: variant): boolean;

    return Kernel.System.varType(value) = TYPE_ARRAY;
  end;

  func var_is_class(value: variant): boolean;

    return Kernel.System.varType(value) = TYPE_CLASS;
  end;

  func var_is_nil(value: variant): boolean;
    
    return Kernel.System.varType(value) = TYPE_NIL;
  end;


  -- проверяет равенство значений
  func var_equal(const value, other_value: variant): boolean;

    return value in [other_value];
  end;

  -- проверяет неравенство значений
  func var_not_equal(const value, other_value: variant): boolean;
    
    return not var_equal(value, other_value);
  end;


  -- проверяет идентичность значений, возвращает истину если значения и типы переменных совпадают
  func var_identical(value, other_value: variant): boolean;
    
    return Kernel.System.varType(value) = Kernel.System.varType(other_value) and var_equal(value, other_value);
  end;

  -- проверяет неидентичность
  func var_not_identical(value, other_value: variant): boolean;
    
    return not var_identical(value, other_value);
  end;


  -- проверяет принадлежность значения к типу, в качестве аргумента expected необходимо передать соответствующую константу класса
  func var_type_is(value: variant; expected: var_type): boolean;

    return Kernel.System.varType(value) = expected;
  end;

  func var_class_is(value: variant; expected: class): boolean;
    
    return (var_is_class(value) or var_is_object(value)) and value.inheritsFrom(expected);
  end;

  -- проверяет для значения принадлежность к типу объект и к классу expected
  func var_instance_of(value: variant; expected: class): boolean;
    
    result = (var_is_object(value) and value.inheritsFrom(expected));
  end;



  -- обменивает значения переменных
  proc var_swap(var variable: variant; var other_variable: variant);
    
    var temp: variant;
        temp = variable;

    variable = other_variable;
    other_variable = temp;
  end;
end