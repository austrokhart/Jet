class "Класс для хранения результатов выполнения тестовой ситуации";
import classes Functions.String, Functions.Array;

inclass


  -- создаёт новый экземпляр класса
  func new(_name: string = nil; _asserts: UnitTesting.AssertResult[] = []; _comment: string = ""): UnitTesting.CaseResult;
    
    result = self.create;

    result._name    = _name;
    result._asserts = _asserts;
    result._comment = _comment;

    return result;
  end;

inobject


  var _name:    string;
  var _asserts: UnitTesting.AssertResult[];
  var _comment: string;
  var _time:    real;


  -- добавляет результат проверки в результаты
  func add_assert(_assert: UnitTesting.AssertResult): UnitTesting.CaseResult;

    array_append(self._asserts, _assert);
    return self;
  end;


  func took(_time: real): UnitTesting.CaseResult;
    
    self._time = _time;
    return self;
  end;


  -- возвращает истину, если результаты успешены
  func is_successful: boolean;
    
    with array_each(self._asserts) do
      while __next do
        if not __item.is_successful then return false; end;
      end;
    end;

    return true;
  end;


  -- возвращает количество проверок в результатах
  func assert_count: integer;

    return array_length(self._asserts);
  end;


  -- возвращает успешные проверки из результатов
  func successful_asserts: UnitTesting.AssertResult[];

    with array_filter(self._asserts) do
      while __next do __apply(__item.is_successful); end; __assign(result);
    end;

    return result;
  end;


  -- возвращает неудачные проверки из результатов
  func unsuccessful_asserts: UnitTesting.AssertResult[];
    
    with array_filter(self._asserts) do
      while __next do __apply(not __item.is_successful); end; __assign(result);
    end;

    return result;
  end;


  -- возвращает текстовое представление случая
  func to_text: string[];

    if self.is_successful then
      array_append(result, str_format("случай {} ({})... {}", [self._name, self._comment, "успех"]));

    else
      array_append(result, str_format("случай {} ({})... {}", [self._name, self._comment, "неудача"]));

      with array_each(self._asserts) do
        while __next do array_append(result, str_format("..{}. {}", [__index, __item.to_text])); end;
      end;
    end;

    return result;
  end;
end