class "Класс для хранения результатов выполнения тестового набора";
import classes Functions.String, Functions.Array;

inclass


  -- возвращает новый экземпляр класса
  func new(_name: string; _cases: UnitTesting.CaseResult[] = []; _comment: string = ""): UnitTesting.SuiteResult;
    
    result = self.create;

    result._name    = _name;
    result._cases   = _cases;
    result._comment = _comment;

    return result;
  end;

inobject


  var _name:    string;
  var _cases:   UnitTesting.CaseResult[];
  var _comment: string;
  var _time:    real;


  -- добавляет результат тестового случая в результаты
  func add(_case: UnitTesting.CaseResult): UnitTesting.SuiteResult;
    
    array_append(self._cases, _case);
    return self;
  end;


  func took(_time: real): UnitTesting.SuiteResult;
    
    self._time = _time;
    return self;
  end;


  -- возвращает истину, если результаты успешны
  func is_successful: boolean;
    
    with array_each(self._cases) do
      while __next do
        if not __item.is_successful then return false; end;
      end;
    end;

    return true;
  end;


  -- возвращает количество тестовых случаев в результатах
  func case_count: integer;

    return array_length(self._cases);
  end;


  func assert_count: integer;
    
    with array_reduce(self._cases, 0) do
      while __next do __apply(__result +__item.assert_count); end; __assign(result);
    end;

    return result;
  end;


  -- возвращает успешные тестовые случаи из результатов
  func successful_cases: UnitTesting.CaseResult[];
    
    with array_filter(self._cases) do
      while __next do __apply(__item.is_successful); end; __assign(result);
    end;

    return result;
  end;


  -- возвращает неудачные тестовые случаи из результатов
  func unsuccessful_cases: UnitTesting.CaseResult[];
    
    with array_filter(self._cases) do
      while __next do __apply(not __item.is_successful); end; __assign(result);
    end;

    return result;
  end;


  -- возвращает текстовое представление набора
  func to_text: string[];

    array_append(result, str_format("набор {} ({}):", [self._name, self._comment]));

    with array_each(self._cases) do
      while __next do

        with array_each(__item.to_text) do
          while __next do array_append(result, str_format("..{}", __item)); end;
        end;
      end;
    end;

    return result;
  end;
end